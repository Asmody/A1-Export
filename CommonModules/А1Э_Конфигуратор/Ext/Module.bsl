#Если Клиент Тогда
	
	// Сохраняет конфигурацию в файл (cf/cfe)
	//
	// Параметры:
	//  Пароль				 - Строка - по умолчанию - для текущего пользователя. 
	//  ПолноеИмяФайла		 - Строка - имя файла (включая путь), в который будет выполнено сохранение. 
	//  ИмяРасширения		 - Строка - имя расширения, которое будет сохранено. По умолчанию сохраняется конфигурация основной базы.
	//  КонтекстВыполнения	 - Структура,Неопределено - см. РабочийКонтекстВыполнения(). 
	// 
	// Возвращаемое значение:
	//   - Неопределено 
	//
	Функция СохранитьКонфигурациюВФайл(Пароль, ПолноеИмяФайла, ИмяРасширения = Неопределено, Знач КонтекстВыполнения = Неопределено) Экспорт
		РабочийКонтекст = РабочийКонтекстВыполнения(Пароль, КонтекстВыполнения);
		
		КонтекстКоманды = Новый Массив;
		КонтекстКоманды.Добавить("""" + ПолноеИмяФайла + """");
		Если ЗначениеЗаполнено(ИмяРасширения) Тогда
			КонтекстКоманды.Добавить("-Extension " + ИмяРасширения);
		КонецЕсли;
		
		ВыполнитьКоманду("DumpCfg", РабочийКонтекст, КонтекстКоманды);
	КонецФункции 
	
	// Функция - Сохранить конфигурацию в файлы (xml/bsl)
	//
	// Параметры:
	//  Пароль				 - Строка - 
	//  ПолныйПутьСохранения - Строка - 
	//  ИмяРасширения		 - Строка,Булево - если указана строка, то сохраняет соответствующее расширение. Если Истина, то сохраняет все расширения. 
	//  ИерархическийФормат  - Булево - указывает, использовать ли более новый иерархический формат.
	//  КонтекстВыполнения	 - Структура,Неопределено - см. РабочийКонтекстВыполнения(). 
	// 
	// Возвращаемое значение:
	//   -  
	//
	Функция СохранитьКонфигурациюВФайлы(Пароль, ПолныйПутьСохранения, ИмяРасширения = Неопределено, ИерархическийФормат = Истина, Знач КонтекстВыполнения = Неопределено) Экспорт
		РабочийКонтекст = РабочийКонтекстВыполнения(Пароль, КонтекстВыполнения);
		КонтекстКоманды = Новый Массив;
		КонтекстКоманды.Добавить("""" + ПолныйПутьСохранения + """");
		Если ЗначениеЗаполнено(ИмяРасширения) Тогда
			Если ИмяРасширения = Истина Тогда
				КонтекстКоманды.Добавить("-AllExtensions");
			Иначе
				КонтекстКоманды.Добавить("-Extension " + ИмяРасширения);
			КонецЕсли;
		КонецЕсли;
		Если ИерархическийФормат = Ложь Тогда
			КонтекстКоманды.Добавить("-Format Plain");
		КонецЕсли;
		ВыполнитьКоманду("DumpConfigToFiles", РабочийКонтекст, КонтекстКоманды);
	КонецФункции 
	
	Функция Открыт() Экспорт
		Возврат А1Э_Пользователи.ОткрытКонфигуратор();
	КонецФункции 
#КонецЕсли

#Область Служебно
#Если Клиент Тогда
	
	Функция ВыполнитьКоманду(ИмяКоманды, КонтекстВыполнения, КонтекстКоманды = Неопределено)
		Если А1Э_Структуры.ЗначениеСвойства(КонтекстВыполнения.Контекст, "ПоказатьСкрипт") = Истина Тогда
			Сообщить(ТекстКоманды(ИмяКоманды, КонтекстВыполнения, КонтекстКоманды));
			Возврат Неопределено;
		КонецЕсли;
		Если КонтекстВыполнения.СтрокаСоединения = СтрокаСоединенияИнформационнойБазы() Тогда
			Если НЕ А1Э_Пользователи.ПарольСоответствует(КонтекстВыполнения.Пароль, КонтекстВыполнения.ИмяПользователя) Тогда
				Сообщить("Указан неверный пароль!");
				Возврат Ложь;
			КонецЕсли;
			Если А1Э_Пользователи.ОткрытКонфигуратор() Тогда
				Сообщить("Для выполнения команды необходимо закрыть конфигуратор!");
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		А1Э_ОбщееКлиент.ВыполнитьСкрипт(ТекстКоманды(ИмяКоманды, КонтекстВыполнения, КонтекстКоманды));
	КонецФункции
	
	Функция ТекстКоманды(Знач ИмяКоманды, Знач КонтекстВыполнения, Знач КонтекстКоманды = Неопределено) Экспорт  
		Если ТипЗнч(КонтекстВыполнения) = Тип("Строка") Тогда //Это пароль
			КонтекстВыполнения = НовыйКонтекстВыполнения(КонтекстВыполнения);	
		КонецЕсли;
		
		ПараметрыКоманды = "";
		Для Каждого Элемент Из КонтекстКоманды Цикл
			А1Э_Строки.Добавить(ПараметрыКоманды, Элемент, " ");
		КонецЦикла;
		
		Результат = А1Э_Строки.ПоШаблону(
		"""[ПутьК1С]"" DESIGNER /DisableStartupMessages /DisableStartupDialogs /IBConnectionString ""[СтрокаСоединения]"" /N ""[ИмяПользователя]"" /P ""[Пароль]"" /[ИмяКоманды] [ПараметрыКоманды]", 
		А1Э_Структуры.Создать(
		"ПутьК1С", КонтекстВыполнения.ПутьК1С,
		"СтрокаСоединения", СтрЗаменить(КонтекстВыполнения.СтрокаСоединения, """", """"""),
		"ИмяПользователя", КонтекстВыполнения.ИмяПользователя,
		"Пароль", КонтекстВыполнения.Пароль,
		"ИмяКоманды", ИмяКоманды,
		"ПараметрыКоманды", ПараметрыКоманды,
		));
		Возврат Результат;
	КонецФункции
	
	Функция НовыйКонтекстВыполнения(Пароль, ПутьК1С = Неопределено, СтрокаСоединения = Неопределено, ИмяПользователя = Неопределено, Контекст = Неопределено)  
		КонтекстВыполнения = А1Э_Структуры.Создать(
		"ПутьК1С", ПутьК1С,
		"СтрокаСоединения", СтрокаСоединения,
		"ИмяПользователя", ИмяПользователя,
		"Пароль", Пароль,
		"Контекст", Контекст,
		);
		Если КонтекстВыполнения.ПутьК1С = Неопределено Тогда
			КонтекстВыполнения.ПутьК1С = "C:\Program Files\1cv8\" + А1Э_Общее.СистемнаяИнформация().ВерсияПриложения + "\bin\1cv8.exe";
		КонецЕсли;
		Если КонтекстВыполнения.СтрокаСоединения = Неопределено Тогда
			КонтекстВыполнения.СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();
		КонецЕсли;
		Если КонтекстВыполнения.ИмяПользователя = Неопределено Тогда
			КонтекстВыполнения.ИмяПользователя = А1Э_Пользователи.ИмяТекущегоПользователяИБ();
		КонецЕсли;
		Возврат КонтекстВыполнения;
	КонецФункции
	
	// Функция - Рабочий контекст выполнения
	//
	// Параметры:
	//  Пароль				 - Строка - пароль для входа в базу данных.
	//  КонтекстВыполнения	 - Структура,Неопределено - может содержать ключи "ПутьК1С", "СтрокаСоединения", "ИмяПользователя", "Пароль".
	//  	Все ключи кроме пароля по умолчанию заполняются для текущего пользователя текущей ИБ и стандартным путем установки 1С.
	//		Указанный в КонтекстеВыполнения пароль заменяет пароль из первого параметра.
	// 
	// Возвращаемое значение:
	//   - 
	//
	Функция РабочийКонтекстВыполнения(Пароль, КонтекстВыполнения = Неопределено) Экспорт
		Результат = НовыйКонтекстВыполнения(Пароль);
		Если КонтекстВыполнения = Неопределено Тогда Возврат Результат; КонецЕсли;
		ЗаполнитьЗначенияСвойств(Результат, КонтекстВыполнения);
		Возврат Результат;
	КонецФункции 
#КонецЕсли

 
#КонецОбласти  

Функция ИмяМодуля() Экспорт
	Возврат	"А1Э_Конфигуратор";
КонецФункции 