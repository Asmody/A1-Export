// Добавляет значения в массив.
//
// Параметры:
//  Массив	 - Массив	 - 
//  П1		 - Произвольный	 - 
//  П2		 - 	 - 
//  П3		 - 	 - 
//  П4		 - 	 - 
//  П5		 - 	 - 
//  П6		 - 	 - 
//  П7		 - 	 - 
//  П8		 - 	 - 
//  П9		 - 	 - 
//  П10		 - 	 - 
// 
// Возвращаемое значение:
//   - Массив
//
Функция ДобавитьЗначения(Массив, П1 = Null, П2 = Null, П3 = Null, П4 = Null, П5 = Null, П6 = Null, П7 = Null, П8 = Null, П9 = Null, П10 = Null, П11 = Null, П12 = Null, П13 = Null, П14 = Null, П15 = Null) Экспорт 
	Если П1 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П1);
	Если П2 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П2);
	Если П3 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П3);
	Если П4 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П4);
	Если П5 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П5);
	Если П6 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П6);
	Если П7 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П7);
	Если П8 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П8);
	Если П9 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П9);
	Если П10 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П10);
	Если П11 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П11);
	Если П12 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П12);
	Если П13 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П13);
	Если П14 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П14);
	Если П15 = Null Тогда Возврат Массив; КонецЕсли;
	Массив.Добавить(П15);
	
	Возврат Массив;
КонецФункции 

// Создает массив из значений. 
//
// Параметры:
//  П1	 - Произвольный	 - 
//  П2	 - 	 - 
//  П3	 - 	 - 
//  П4	 - 	 - 
//  П5	 - 	 - 
//  П6	 - 	 - 
//  П7	 - 	 - 
//  П8	 - 	 - 
//  П9	 - 	 - 
//  П10	 - 	 - 
// 
// Возвращаемое значение:
//   - 
//
Функция Создать(П1 = Null, П2 = Null, П3 = Null, П4 = Null, П5 = Null, П6 = Null, П7 = Null, П8 = Null, П9 = Null, П10 = Null, П11 = Null, П12 = Null, П13 = Null, П14 = Null, П15 = Null) Экспорт
	Массив = Новый Массив;
	Возврат ДобавитьЗначения(Массив, П1, П2, П3, П4, П5, П6, П7, П8, П9, П10, П11, П12, П13, П14, П15);
КонецФункции

Функция ИзСтроки(Строка, ТипЗначения) Экспорт 
	МассивСтрок = А1Э_Строки.Разделить(Строка,",");
	Массив = Новый Массив;
	Для Каждого СтрокаМассива Из МассивСтрок Цикл
		Если ТипЗначения = Тип("Число") Тогда
			Массив.Добавить(Число(СтрокаМассива));
		Иначе
			ВызватьИсключение "Тип " + ТипЗначения + " не поддерживается функцией А1Э_Массивы.ИзСтроки!";
		КонецЕсли;
	КонецЦикла;
	Возврат Массив;
КонецФункции

Функция Содержит(Массив, Значение) Экспорт
	Возврат Массив.Найти(Значение) <> Неопределено;
КонецФункции

// Возвращает Массив, соответствующий переданным данным. Если передана строка, то она разделяется по ",".
//
// Параметры:
//  Данные	 - Массив,ФиксированныйМассив,Строка - 
// 
// Возвращаемое значение:
//   - 
//
Функция Массив(Данные) Экспорт
	Если ТипЗнч(Данные) = Тип("Массив") Тогда
		Возврат Данные;
	ИначеЕсли ТипЗнч(Данные) = Тип("ФиксированныйМассив") Тогда
		Возврат Новый Массив(Данные); 
	ИначеЕсли ТипЗнч(Данные) = Тип("Строка") Тогда
		Возврат А1Э_Строки.Разделить(Данные, ",");
	ИначеЕсли Данные = Неопределено Тогда
		Возврат Новый Массив;
	Иначе
		Возврат Создать(Данные);
	КонецЕсли;
КонецФункции

Функция МассивКлючей(Структура) Экспорт
	МассивКлючей = Новый Массив;
	Для Каждого Пара Из Структура Цикл
		МассивКлючей.Добавить(Пара.Ключ);
	КонецЦикла;
	Возврат МассивКлючей;
КонецФункции

// Если первый параметр - массив, то возвращает его. Иначе создает массив из параметров 
// 
// Возвращаемое значение:
//   - Массив
//
Функция ИзПараметров(П1 = Null, П2 = Null, П3 = Null, П4 = Null, П5 = Null, П6 = Null, П7 = Null, П8 = Null, П9 = Null, П10 = Null, П11 = Null, П12 = Null, П13 = Null, П14 = Null, П15 = Null) Экспорт
	Если ЯвляетсяМассивом(П1) Тогда Возврат П1; КонецЕсли;
	Возврат Создать(П1, П2, П3, П4, П5, П6, П7, П8, П9, П10, П11, П12, П13, П14, П15);
КонецФункции

// Возвращает единственный элемент массива. Если массив пустой возвращает Неопределено. Если в массиве более 1 элемента вызывается исключение.
//
// Параметры:
//  Массив	 - Массив - 
// 
// Возвращаемое значение:
//   - Произвольный
//
Функция ЕдинственныйЭлемент(Массив) Экспорт
	#Если Сервер И НЕ Сервер Тогда
		Массив = Новый Массив;		
	#КонецЕсли
	
	Если Массив.Количество() = 0 Тогда
		Возврат Неопределено;
	ИначеЕсли Массив.Количество() > 1 Тогда
		А1Э_Служебный.СлужебноеИсключение("В массиве более одного элемента!");
	Иначе
		Возврат Массив[0];
	КонецЕсли;
	
КонецФункции 

Функция Сортировать(Массив, Знач Направление = "Возр") Экспорт
	Направление = А1Э_СтандартныеТипы.НаправлениеСортировкиПолучить(Направление);
	
	Список = Новый СписокЗначений;
    Список.ЗагрузитьЗначения(Массив);
	Список.СортироватьПоЗначению(Направление);    
	Массив = Список.ВыгрузитьЗначения();
КонецФункции

Функция Скопировать(Массив) Экспорт
	НовыйМассив = Новый Массив;
	Для Каждого Элемент Из Массив Цикл
		НовыйМассив.Добавить(Элемент);
	КонецЦикла;
	Возврат НовыйМассив;	
КонецФункции

Функция Получить(Массив, Индекс) Экспорт
	Если Индекс < Массив.Количество() Тогда
		Возврат Массив[Индекс];
	КонецЕсли;
	Возврат Неопределено;
КонецФункции 

Функция Добавить(Массив, Источник) Экспорт
	МассивДобавления = Массив(Источник);	
	Для Каждого Элемент Из МассивДобавления Цикл
		Массив.Добавить(Элемент);
	КонецЦикла;
	Возврат Массив;
КонецФункции

Функция ДобавитьЕслиОтсутствует(Массив, Источник) Экспорт
	МассивДобавления = Массив(Источник);
	Для Каждого Элемент Из МассивДобавления Цикл
		Если Массив.Найти(Элемент) = Неопределено Тогда
			Массив.Добавить(Элемент);
		КонецЕсли;
	КонецЦикла;
	Возврат Массив;
КонецФункции

Функция ДобавитьНепустой(Массив, Источник) Экспорт
	Если А1Э_СтандартныеТипы.Заполнено(Источник) Тогда
		Массив.Добавить(Источник);
	КонецЕсли;
КонецФункции

Функция Последний(Массив) Экспорт
	Если Массив.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	Возврат Массив[Массив.Количество() - 1];	
КонецФункции

Функция ЭлементСКонца(Массив, Индекс = 0) Экспорт
	Если Массив.Количество() <= Индекс Тогда
		А1Э_Служебный.СлужебноеИсключение("Количество элементов в массиве меньше индекса!");
	КонецЕсли;
	Возврат Массив[Массив.Количество() - 1 - Индекс];
КонецФункции

Функция Свернуть(Массив) Экспорт
	Результат = Новый Массив;
	Соответствие = Новый Соответствие;
	Для Каждого Элемент Из Массив Цикл
		Если Соответствие[Элемент] <> Неопределено Тогда Продолжить; КонецЕсли;
		Результат.Добавить(Элемент);
		Соответствие.Вставить(Элемент, Истина);
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция СвернутьВСоответствие(Массив) Экспорт
	Результат = Новый Соответствие;
	Для Каждого Элемент Из Массив Цикл
		ТекущийСчетчик = Результат[Элемент];
		Если ТекущийСчетчик = Неопределено Тогда
			Результат.Вставить(Элемент, 1);
		Иначе
			Результат.Вставить(Элемент, ТекущийСчетчик + 1);
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ЯвляетсяМассивом(Значение) Экспорт
	Возврат ТипЗнч(Значение) = Тип("Массив") Или ТипЗнч(Значение) = Тип("ФиксированныйМассив");	
КонецФункции

Функция УдалитьЗначение(Массив, Значение, ИсключениеПриОтсутствии = Ложь) Экспорт
	Индекс = Массив.Найти(Значение);
	Если Индекс = Неопределено Тогда
		Если ИсключениеПриОтсутствии Тогда
			А1Э_Служебный.СлужебноеИсключение("Ошибка при попытке удаления элемента из массива: элемент отсутствует!");
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	Массив.Удалить(Индекс);
КонецФункции

Функция ПереместитьЗначениеВКонец(Массив, Значение) Экспорт
	Индекс = Массив.Найти(Значение);
	Если Индекс = Неопределено Тогда Возврат Неопределено; КонецЕсли;
	Массив.Удалить(Индекс);
	Массив.Добавить(Значение);		
КонецФункции

// Возвращает Соответствие вида <Тип>:<Массив>(значений этого типа).
//
// Параметры:
//  Массив	 - Массив	 - 
// 
// Возвращаемое значение:
//   - Соответствие
//
Функция РазбитьПоТипам(Массив) Экспорт
	Соответствие = Новый Соответствие;
	Для Каждого Элемент Из Массив Цикл
		А1Э_Иерархии.ДобавитьВСоответствиеМассивов(Соответствие, ТипЗнч(Элемент), Элемент);
	КонецЦикла;
	Возврат Соответствие;
КонецФункции 
