// Конструктор параметров подключения. Возвращает структуру со следующими свойствами:
//  Класс - Строка - "ДанныеПодключения1С"
//  ВерсияПлатформы - Строка - Версия платформы, например "8.3.13.1666".
//  ИмяКоннектора - Строка - для создания COM-объекта, например "V83.COMConnector"
//  ЭтоСервер - Булево - Истина если база серверная.
//  СтрокаПодключения - строка для подключения.
//  Сервер - Строка - имя сервера 1С. Пустая строка если база файловая.
//  ИмяБазы - Строка - имя базы на сервере 1С. Пустая строка если база файловая.
//  ПутьКБазе - Строка - путь к файловой базе. Пустая строка если база серверная.
//  ИмяПользователя - имя пользователя для входа в базу.
//  Пароль - пароль для входа в базу.
// Параметры:
//  СерверПутьСтрокаПодключения	 - Строка - или строка подключения, или имя сервера, или путь к базе 
//  ИмяБазы						 - Строка - оставить пустой если база файловая
//  ИмяПользователя				 - Строка - 
//  Пароль						 - Строка - 
//  ВерсияПлатформы				 - Строка - рекомендуется указывать "8.3", "8.2" и т.д.
// 
// Возвращаемое значение:
//   - 
//
Функция СоздатьДанныеПодключения(СерверПутьСтрокаПодключения, ИмяБазы = "", ИмяПользователя = "", Пароль = "", ВерсияПлатформы = "8.3") Экспорт
	ДанныеПодключения = А1Э_Классы.НовыйДанныеПодключенияК1С();
	ДанныеПодключения.ВерсияПлатформы = ВерсияПлатформы;
	ДанныеПодключения.ИмяКоннектора = "V" + СтрЗаменить(Лев(ВерсияПлатформы, 3),".","") +".COMConnector";
	
	Если СтрНайти(СерверПутьСтрокаПодключения,"""") > 0 Тогда
		РазобратьСтрокуПодключения(СерверПутьСтрокаПодключения, ДанныеПодключения);
	Иначе
		Если ИмяБазы <> "" Тогда
			ДанныеПодключения.ЭтоСервер = Истина;
			ДанныеПодключения.Сервер = СерверПутьСтрокаПодключения;
			ДанныеПодключения.Имябазы = ИмяБазы;
		Иначе
			ДанныеПодключения.ЭтоСервер = Ложь;
			ДанныеПодключения.ПутьКБазе = СерверПутьСтрокаПодключения;
		КонецЕсли;
		ДанныеПодключения.ИмяПользователя = ИмяПользователя;
		ДанныеПодключения.Пароль = Пароль;
		СобратьСтрокуПодключения(ДанныеПодключения);		
	КонецЕсли;
	
	Возврат ДанныеПодключения;
		
КонецФункции

Функция СоздатьПодключение(ДанныеПодключения) Экспорт
	Попытка
		ОбъектПодключения = Новый COMObject(ДанныеПодключения.ИмяКоннектора); 
		Подключение = ОбъектПодключения.Connect(ДанныеПодключения.СтрокаПодключения);
		Возврат Подключение;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ВызватьИсключение ОписаниеОшибки;
	КонецПопытки;
КонецФункции

Функция РазобратьСтрокуПодключения(СтрокаПодключения, ДанныеПодключения = Неопределено)
	Если ДанныеПодключения = Неопределено Тогда
		ДанныеПодключения = А1Э_Классы.НовыйДанныеПодключенияК1С();
	КонецЕсли;
	
	Если СтрНайти(СтрокаПодключения,"Srvr") > 0 Тогда
		ДанныеПодключения.ЭтоСервер = Истина;
	ИначеЕсли СтрНайти(СтрокаПодключения, "File") > 0 Тогда
		ДанныеПодключения.ЭтоСервер = Ложь;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Если ДанныеПодключения.ЭтоСервер Тогда
		ДанныеПодключения.Сервер = ПолучитьЗначениеВСтрокеПодключения(СтрокаПодключения, "Srvr");
		ДанныеПодключения.ИмяБазы = ПолучитьЗначениеВСтрокеПодключения(СтрокаПодключения, "Ref");
	Иначе
		ДанныеПодключения.ПутьКБазе = ПолучитьЗначениеВСтрокеПодключения(СтрокаПодключения, "File");
	КонецЕсли;
	
	ДанныеПодключения.ИмяПользователя = ПолучитьЗначениеВСтрокеПодключения(СтрокаПодключения, "Usr=");
	ДанныеПодключения.Пароль = ПолучитьЗначениеВСтрокеПодключения(СтрокаПодключения, "Pwd");
	
	ДанныеПодключения.СтрокаПодключения = СтрокаПодключения;
	Возврат ДанныеПодключения;
КонецФункции

Функция СобратьСтрокуПодключения(ДанныеПодключения) 
	СтрокаПодключения = "";
	Если ДанныеПодключения.ЭтоСервер Тогда
		ДобавитьЗначениеВСтрокуПодключения(СтрокаПодключения, "Srvr", ДанныеПодключения.Сервер);
		ДобавитьЗначениеВСтрокуПодключения(СтрокаПодключения, "Ref", ДанныеПодключения.ИмяБазы);
	Иначе
		ДобавитьЗначениеВСтрокуПодключения(СтрокаПодключения, "File", ДанныеПодключения.ПутьКБазе);
	КонецЕсли;
	
	ДобавитьЗначениеВСтрокуПодключения(СтрокаПодключения, "Usr", ДанныеПодключения.ИмяПользователя);
	ДобавитьЗначениеВСтрокуПодключения(СтрокаПодключения, "Pwd", ДанныеПодключения.Пароль);
	
	ДанныеПодключения.СтрокаПодключения = СтрокаПодключения;
КонецФункции

Функция ПолучитьЗначениеВСтрокеПодключения(СтрокаПодключения, КлючЗначения)
	Возврат А1Э_Строки.ПодстрокаМежду(СтрокаПодключения, КлючЗначения + """=", """");	
КонецФункции

Функция ДобавитьЗначениеВСтрокуПодключения(СтрокаПодключения, Ключ, Значение)
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Возврат Неопределено;
	КонецЕсли;
	СтрокаПодключения = СтрокаПодключения + (Ключ + "=""" + Значение + """;");
	Возврат Истина;
КонецФункции

