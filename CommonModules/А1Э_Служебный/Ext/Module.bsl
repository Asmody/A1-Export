#Область Исключения

Функция СлужебноеИсключение(ТекстОшибки) Экспорт
	ТекстОшибки = "Программная ошибка: " + ТекстОшибки;
	Если ЭтоАдминистраторА1() Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	МассивКонтактов = А1Э_Общее.МассивКонтактов();
	Если МассивКонтактов.Количество() = 0 Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ТекстОшибки = ТекстОшибки + Символы.ПС + Символы.ПС + 
	"Если Вы не понимаете, что написано выше, и не знаете к кому обратиться,
	|то Вы можете связаться с нами следующими способами:" + Символы.ПС;
	Для Каждого Контакт Из МассивКонтактов Цикл
		ТекстОшибки = ТекстОшибки + Контакт.Тип + ": " + Контакт.Значение + Символы.ПС;
	КонецЦикла;
	
	ВызватьИсключение ТекстОшибки;
КонецФункции

// Вызывает исключение при несоответствии типов
//
// Параметры:
//  ИмяПеременной	 - Строка - 
//  ИмяФункции		 - Строка - 
//  Значение		 - ЛюбоеЗначение - будет приведено к строке для вывода 
//  Типы			 - Строка - можно несколько через запятую. 
// 
// Возвращаемое значение:
//   - 
//
Функция ИсключениеНеверныйТип(ИмяПеременной, ИмяФункции, Значение, Типы) Экспорт
	СлужебноеИсключение("В переменную <" + ИмяПеременной + "> было передано неверное значение " + Строка(Значение) + 
	"! Функция " + ИмяФункции + " поддерживает типы " + Типы);	
КонецФункции

Функция ИсключениеНеверныйКласс(ИмяПеременной, ИмяФункции, Значение, Классы) Экспорт
	СлужебноеИсключение("В переменную <" + ИмяПеременной + "> было передано неверное значение " + Строка(Значение) + 
	"! Функция " + ИмяФункции + " поддерживает классы " + Классы);	
КонецФункции 

#КонецОбласти 

#Область Сериализация

Функция СлужебноеПредставление(ИсточникДанных) Экспорт
	#Если НЕ ВебКлиент Тогда
		Попытка
			Запись = Новый ЗаписьJSON;
			Запись.УстановитьСтроку();
			СериализаторXDTO.ЗаписатьJSON(Запись, ИсточникДанных, НазначениеТипаXML.Явное);
			Возврат Запись.Закрыть();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Возврат ОписаниеОшибки;
		КонецПопытки;
	#Иначе
		Возврат "Служебное представление не может быть получено в веб-клиенте!";
	#КонецЕсли
КонецФункции

#КонецОбласти

Функция ЭтоАдминистраторА1() Экспорт
	ИмяПользователя = А1Э_ОбщееСервер.ИмяСистемногоПользователя();
	Если СтрНайти(ИмяПользователя, "А1_") Тогда
		Возврат Истина;
	КонецЕсли;
	Возврат Ложь; 	
КонецФункции

#Область ОтказЗаписиПроведенияОбъектов

Функция УстановитьОтказ(Объект, Отказ, Причина = Неопределено) Экспорт 
	Отказ = Истина;
	Если НЕ А1Э_БСП.РежимОтладки() Тогда
		Если ЗначениеЗаполнено(Причина) Тогда
			Сообщить(Причина);
		КонецЕсли;
		Возврат Неопределено;
	КонецЕсли;
	ВызватьИсключение Причина;	
КонецФункции

#КонецОбласти 