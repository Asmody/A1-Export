Функция СообщитьСДанными(Текст, Ссылка = Неопределено) Экспорт
	СообщениеСДанными(Текст, Ссылка).Сообщить();
КонецФункции

Функция СообщениеСДанными(Текст, Ссылка = Неопределено) Экспорт
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Если Ссылка <> Неопределено Тогда
		Сообщение.Текст = Сообщение.Текст + Символы.ПС + "Можете нажать дважды для просмотра...";
	КонецЕсли;
	Сообщение.КлючДанных = Ссылка;
	Возврат Сообщение;
КонецФункции

Функция СообщитьМассив(Знач МассивСообщений) Экспорт
	МассивСообщений = А1Э_Массивы.Массив(МассивСообщений);
	
	Для Каждого Элемент Из МассивСообщений Цикл
		Если ТипЗнч(Элемент) = Тип("Строка") Тогда
			Продолжить;
		ИначеЕсли ТипЗнч(Элемент) = Тип("СообщениеПользователю") Тогда
			Продолжить;
		Иначе
			ВызватьИсключение "Элементами массива сообщений могут быть только строки и СообщенияПользователю!";
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Элемент Из МассивСообщений Цикл
		Если ТипЗнч(Элемент) = Тип("Строка") Тогда
			Сообщить(Элемент);
		ИначеЕсли ТипЗнч(Элемент) = Тип("СообщениеПользователю") Тогда
			Элемент.Сообщить();
		КонецЕсли;
	КонецЦикла;
КонецФункции

Функция СообщениеПоШаблону(Шаблон, Структура) Экспорт
	Текст = А1Э_Строки.ПоШаблону(Шаблон, Структура);
	Возврат СообщениеСДанными(Текст, А1Э_Структуры.ЗначениеСвойства(Структура, "Ссылка"));
КонецФункции

Функция СообщитьПоШаблону(Шаблон, Структура) Экспорт
	Сообщение = СообщениеПоШаблону(Шаблон, Структура);
	СообщитьМассив(Сообщение);
	Возврат Сообщение;
КонецФункции

// Создает контекстное сообщение, привязанное к полю формы.
//
// Параметры:
//  Текст		 - Строка - текст сообщения.
//  Ссылка		 - ЛюбаяСсылка - 
//  Поле		 - Строка - имя поля формы.
//  ПутьКДанным	 - Строка - имя реквизита, хранящего объект (обычно "Объект"). 
// 
// Возвращаемое значение:
//   - СообщениеПользователю
//
Функция СообщениеНаФорму(Текст, Ссылка, Поле, ПутьКДанным = "Объект") Экспорт
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.КлючДанных = Ссылка;
	Сообщение.ПутьКДанным = ПутьКДанным;
	Сообщение.Поле = Поле;
	Возврат Сообщение;
КонецФункции

// Вызывает контекстное сообщение, привязанное к полю формы.
//
// Параметры:
//  Текст		 - Строка - текст сообщения.
//  Ссылка		 - ЛюбаяСсылка - 
//  Поле		 - Строка - имя поля формы.
//  ПутьКДанным	 - Строка - имя реквизита, хранящего объект (обычно "Объект"). 
// 
// Возвращаемое значение:
//   - 
//
Функция СообщитьНаФорму(Текст, Ссылка, Поле, ПутьКДанным = "Объект") Экспорт  
	Сообщение = СообщениеНаФорму(Текст, Ссылка, Поле, ПутьКДанным);
	Сообщение.Сообщить()
КонецФункции

// Вызывает контекстное сообщение, привязанной к ячейке табличной части элемента.
//
// Параметры:
//  Текст		 - Строка - текст сообщения
//  Ссылка		 - ЛюбаяСсылка - см. функцию СообщениеНаФорму()
//  ИмяТЧ		 - Строка - имя табличной части.
//  НомерСтроки	 - Число - номер строки (начинается с 1).
//  ИмяКолонки	 - Строка - имя колонки в табличной части.
//  ПутьКДанным	 - Строка - см. функцию СообщениеНаФорму() 
// 
// Возвращаемое значение:
//   - 
//
Функция СообщитьНаТаблицуФормы(Текст, Ссылка, ИмяТЧ, НомерСтроки, ИмяКолонки, ПутьКДанным = "Объект") Экспорт
	Поле = "" + ИмяТЧ + "[" + А1Э_Строки.ВСтроку(НомерСтроки - 1) + "]." + ИмяКолонки;
	Возврат СообщитьНаФорму(Текст, Ссылка, Поле, ПутьКДанным); 
КонецФункции 

#Если НЕ Клиент Тогда
	Функция ПолучитьЛогСообщений() Экспорт
		Лог = Новый Массив;
		МассивСообщений = ПолучитьСообщенияПользователю(Истина);
		Для Каждого Сообщение Из МассивСообщений Цикл
			Если СтрНачинаетсяС(Сообщение.Текст, "[А1_Лог]") Тогда
				Лог.Добавить(Сред(Сообщение.Текст, 9));
			Иначе
				Сообщение.Сообщить();
			КонецЕсли;
		КонецЦикла;
		Возврат Лог;
	КонецФункции 	
#КонецЕсли

