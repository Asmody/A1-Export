#Область ПрограммныйИнтерфейс

// Функция - Файл эксель в массив структур
//
// Параметры:
//  ПутьКФайлу	 - Строка - полный путь к файлу 
//  Параметры	 - Структура - Содержит ключи
//		ОбластьДанных - Структура - Может содержать ключи "ПерваяСтрока", "ПоследняяСтрока", "ПерваяКолонка", "ПоследняяКолонка" с числовыми значениями.
//		СтрокиЗаголовков - Массив, Число - Содержит массив номеров строк (числа), в которых хранятся заголовки колонок.   
//		Лист - Число - номер листа, который будет прочитан. Нумерация в традиции Экселя начинается с 1. По умолчанию 1.
//		НовыйМетод - Булево - определяет, будет ли выполнено чтение методом платформы (доступен с 8.3.10).
// 
// Возвращаемое значение:
//  Массив - массив структур, имитирующий работу таблицы. 
//
Функция ФайлЭксельВТаблицу(ПутьКФайлу, Параметры) Экспорт
	НомерЛиста = А1Э_Структуры.ЗначениеСвойства(Параметры, "Лист", 1);
	НовыйМетод = А1Э_Структуры.ЗначениеСвойства(Параметры, "НовыйМетод", Ложь);
	
	РезультатВыполнения = ОткрытьЭксель(ПутьКФайлу, НомерЛиста, НовыйМетод);
	Если НЕ А1Э_РезультатыФункций.ОбработатьОшибки(РезультатВыполнения, "Запуск Misrosoft Excel")  Тогда
		Возврат Неопределено;
	КонецЕсли;
	Эксель = РезультатВыполнения.Значение;
	
	Попытка
		РабочиеПараметры = Новый Структура;
		РабочиеПараметры.Вставить("ОбластьДанных", ПолучитьОбластьДанных(Эксель, Параметры));
		РабочиеПараметры.Вставить("СтрокиЗаголовков", ПолучитьМассивСтрокЗаголовков(Параметры));
		РабочиеПараметры.Вставить("НовыйМетод", НовыйМетод);
		РабочиеПараметры.Вставить("МассивКолонок", ПолучитьМассивКолонок(Эксель, РабочиеПараметры));
		
		Таблица = Новый Массив;
		
		НомерСтроки = РабочиеПараметры.ОбластьДанных.ПерваяСтрока - 1;
		Пока НомерСтроки < РабочиеПараметры.ОбластьДанных.ПоследняяСтрока Цикл
			НомерСтроки = НомерСтроки + 1;
			Таблица.Добавить(ПрочитатьСтроку(Эксель, РабочиеПараметры.МассивКолонок, НомерСтроки));
		КонецЦикла;
		
		ЗакрытьЭксель(Эксель);
		
		Возврат Таблица;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ЗакрытьЭксель(Эксель);
	КонецПопытки;
	
КонецФункции

#Область ФайлЭксельВТаблицу

Функция ПолучитьОбластьДанных(Эксель, Параметры)
	ОбластьДанных = НовыйОбластьДанных();
	А1Э_Структуры.СкопироватьСвойства(ОбластьДанных, Параметры.ОбластьДанных, Истина);
	
	Если Параметры.НовыйМетод Тогда
		ПоследняяСтрока = Эксель.Область.Низ - Эксель.Область.Верх;
		ПоследняяКолонка = Эксель.ТабличныйДокумент.ШиринаТаблицы;
	Иначе
		ПоследняяЯчейка = Эксель.Cells(1,1).SpecialCells(11);
		ПоследняяСтрока = ПоследняяЯчейка.Row + ПоследняяЯчейка.Rows.Count - 1;
		ПоследняяКолонка = ПоследняяЯчейка.Column + ПоследняяЯчейка.Columns.Count - 1;
	КонецЕсли;
	
	Если ОбластьДанных.ПоследняяСтрока = 0 ИЛИ ОбластьДанных.ПоследняяСтрока > ПоследняяСтрока Тогда
		ОбластьДанных.ПоследняяСтрока = ПоследняяСтрока;
	КонецЕсли;
	Если ОбластьДанных.ПоследняяКолонка = 0 ИЛИ ОбластьДанных.ПоследняяКолонка > ПоследняяКолонка Тогда
		ОбластьДанных.ПоследняяКолонка = ПоследняяКолонка;
	КонецЕсли;
	
	Возврат ОбластьДанных;
КонецФункции

Функция ПолучитьМассивСтрокЗаголовков(Параметры)
	СтрокиЗаголовков = А1Э_Структуры.ЗначениеСвойства(Параметры, "СтрокиЗаголовков");
	
	Если СтрокиЗаголовков = Неопределено Тогда
		МассивСтрокЗаголовков = А1Э_Массивы.Создать(1);
	ИначеЕсли ТипЗнч(СтрокиЗаголовков) = Тип("Число") Тогда
		МассивСтрокЗаголовков = А1Э_Массивы.Создать(СтрокиЗаголовков);
	ИначеЕсли ТипЗнч(СтрокиЗаголовков) = Тип("Массив") Тогда
		МассивСтрокЗаголовков = А1Э_Массивы.Скопировать(СтрокиЗаголовков);
	КонецЕсли;
	#Если Сервер И НЕ Сервер Тогда
		МассивСтрокЗаголовков = Новый Массив;		
	#КонецЕсли 
	А1Э_Массивы.Сортировать(МассивСтрокЗаголовков);
	Возврат МассивСтрокЗаголовков;	
КонецФункции

Функция ПолучитьМассивКолонок(Эксель, РабочиеПараметры)
	МассивКолонок = Новый Массив;
	КоличестваИдентификаторов = Новый Соответствие;
	НомерКолонки = РабочиеПараметры.ОбластьДанных.ПерваяКолонка - 1;
	Пока НомерКолонки < РабочиеПараметры.ОбластьДанных.ПоследняяКолонка Цикл
		НомерКолонки = НомерКолонки + 1;
		СтруктураКолонки = НовыйСтруктураКолонки();
		СтруктураКолонки.Номер = НомерКолонки;
		НаименованиеКолонки = "";
		Для Каждого СтрокаЗаголовка Из РабочиеПараметры.СтрокиЗаголовков Цикл
			ТекстЯчейки = ПрочитатьЯчейку(Эксель, СтрокаЗаголовка, НомерКолонки);
			Если ТекстЯчейки <> "" Тогда
				НаименованиеКолонки = НаименованиеКолонки + ТекстЯчейки + "_";
			КонецЕсли;
		КонецЦикла;
		НаименованиеКолонки = А1Э_Строки.Обрубить(НаименованиеКолонки, 1);
		Идентификатор = А1Э_СтандартныеТипы.Идентификатор(НаименованиеКолонки, "Колонка_" + А1Э_Строки.ВСтроку(НомерКолонки));
		СтруктураКолонки.Имя = А1Э_СтандартныеТипы.ИдентификаторБезПовторений(Идентификатор, КоличестваИдентификаторов);
		МассивКолонок.Добавить(СтруктураКолонки);
	КонецЦикла;
	Возврат МассивКолонок;	
КонецФункции

#КонецОбласти

#КонецОбласти 

#Область ИнтерфейсЭксель

Функция ОткрытьЭксель(ПутьКФайлу, Лист = 1, НовыйМетод = Ложь) Экспорт 
	Если НовыйМетод Тогда //Новый метод читает платформенными средствами в табличный документ.
		ТабДок = Новый ТабличныйДокумент;
		Попытка
			ТабДок.Прочитать(ПутьКФайлу, СпособЧтенияЗначенийТабличногоДокумента.Значение);
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Возврат А1Э_РезультатыФункций.Ошибка("Ошибка при чтении файла Excel методом платформы:" + Символы.ПС + ОписаниеОшибки);
		КонецПопытки;
		Попытка
			Если ТипЗнч(Лист) = Тип("Число") Тогда
				Область = ТабДок.Области[Лист - 1];
			Иначе //Строка
				Область = ТабДок.Область(Лист);
			КонецЕсли;
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			Возврат А1Э_РезультатыФункций.Ошибка("Ошибка при поиске листа файла Excel методом платформы:" + Символы.ПС + ОписаниеОшибки); 
		КонецПопытки;
		Возврат А1Э_РезультатыФункций.Успех(А1Э_Структуры.Создать(
		"ТабличныйДокумент", ТабДок,
		"ИмяОбласти", Область.Имя,
		"Область", Область,
		"Верх", Область.Верх,
		"Лево", Область.Лево,
		));
	КонецЕсли;
	
	Попытка
		Эксель = Новый COMОбъект("Excel.Application");
		Эксель.WorkBooks.Open(ПутьКФайлу);
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		Возврат А1Э_РезультатыФункций.Ошибка("Ошибка при открытии программы Microsoft Excel:" + Символы.ПС + ОписаниеОшибки);
	КонецПопытки;
	
	Попытка
		Эксель.Sheets(Лист).Select(); 
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
		ЗакрытьЭксель(Эксель);
		Возврат А1Э_РезультатыФункций.Ошибка("Ошибка при открытии чтении файла " + ПутьКФайлу + ":" + Символы.ПС + ОписаниеОшибки);
	КонецПопытки;
	
	Возврат А1Э_РезультатыФункций.Успех(Эксель);
КонецФункции 

Функция ЗакрытьЭксель(Эксель) Экспорт
	Если Эксель = Неопределено Тогда Возврат Неопределено; КонецЕсли;
	//Если чтение выполняется новым методом платформы.
	Если ТипЗнч(Эксель) = Тип("Структура") Тогда Возврат Неопределено; КонецЕсли;
	
	Эксель.ActiveWorkbook.Close();
	Эксель.DisplayAlerts = 0; 
	Эксель.Quit();
	Эксель.DisplayAlerts = 1;
	Эксель = Неопределено;
КонецФункции 

Функция ПрочитатьЯчейку(Эксель, Строка, Столбец) Экспорт
	//Если чтение выполняется новым методом платформы.
	Если ТипЗнч(Эксель) = Тип("Структура") Тогда Возврат Эксель.ТабличныйДокумент.Область(Эксель.Верх + Строка - 1, Эксель.Лево + Столбец).Текст; КонецЕсли;
	Возврат Эксель.Cells(Строка, Столбец).Text;	
КонецФункции

Функция ПрочитатьСтроку(Эксель, МассивКолонок, НомерСтроки) Экспорт
	СтруктураСтроки = Новый Структура;
	СтруктураСтроки.Вставить("НомерСтрокиА1", НомерСтроки);
	Для Каждого СтруктураКолонки Из МассивКолонок Цикл
		Значение = ПрочитатьЯчейку(Эксель, НомерСтроки, СтруктураКолонки.Номер);
		СтруктураСтроки.Вставить(СтруктураКолонки.Имя, Значение); 
	КонецЦикла;
	Возврат СтруктураСтроки;
КонецФункции

#КонецОбласти

#Область Декларации

Функция НовыйОбластьДанных()
	Возврат Новый Структура("ПерваяСтрока,ПоследняяСтрока,ПерваяКолонка,ПоследняяКолонка",1,0,1,0);
КонецФункции

Функция НовыйСтруктураКолонки()
	Возврат Новый Структура("Имя,Номер");	
КонецФункции

#КонецОбласти